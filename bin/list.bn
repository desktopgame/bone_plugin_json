element_class := object();
element_class.new := def()(ret) {
    ret.prev := false;
    ret.next := false;
    ret.data := false;
    ret.append := def(self, data)() {
        if(self.data) {
            iter := self;
            while(iter.next) {
                iter := iter.next;
            }
            iter.next := element_class.new();
            iter.next.data := data;
        } else {
            self.data := data;
        }
    };
};


append := def(self, data)() {
    self.head.append(data);
};
length := def(self)(ret) {
    ret := 0;
    iter := self.head;
    while(iter) {
        ret += 1;
        iter := iter.next;
    }
};
foreach := def(self, f)() {
    iter := self.head;
    while(iter) {
        f(iter.data);
        iter := iter.next;
    }
};

toArray := def(self)(ret) {
    ret := array(self.length());
    iter := self.head;
    i := 0;
    while(iter) {
        ret[i] := iter.data;
        iter := iter.next;
        i += 1;
    }
};

opLeftShift := def(self, a)() {
    append(self, a);
};

new := def()(ret) {
    ret.head := element_class.new();
    ret.append := append;
    ret.length := length;
    ret.foreach := foreach;
    ret.toArray := toArray;
    ret.opLeftShift := opLeftShift;
};